apply plugin: 'com.android.application'

def buildVersionCode = new Date().format("yyMMddHHmm", TimeZone.getTimeZone("Europe/Istanbul")).toInteger()
def buildVersionName = "1.0"

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.arc"
        minSdkVersion 22
        targetSdkVersion 27
        versionCode buildVersionCode
        versionName buildVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "API_KEY", "\"" + API_KEY + "\"")
        buildConfigField("String", "END_POINT", "\"https://newsapi.org/v2/\"")
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }

    flavorDimensions 'dimension'

    productFlavors {
        internal {
            applicationId 'com.example.arc.internal'
            resValue "string", "app_name", '"Arc Internal"'
        }

        production {
            applicationId 'com.example.arc'
            resValue "string", "app_name", '"Arc"'
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def supportVersion = "27.0.2"
    def daggerVersion = "2.14.1"
    def okHttpVersion = "3.9.1"
    def interceptorVersion = "2.0.4"
    def retrofitVersion = "2.3.0"
    def archVersion = "1.0.0"
    def rxAndroidVersion = "2.0.1"
    def rxJavaVersion = "2.1.7"
    def constVersion = "1.0.2"
    def glideVersion = "4.4.0"
    def espressoVersion = "3.0.1"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //noinspection GradleCompatible,Support
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support.constraint:constraint-layout:$constVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"

    //ArcComponents
    implementation "android.arch.lifecycle:extensions:$archVersion"
    implementation "android.arch.persistence.room:runtime:$archVersion"
    implementation "android.arch.lifecycle:runtime:$archVersion"
//    implementation "android.arch.persistence.room:rxjava2:$archVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$archVersion"

    //Network
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"
    implementation("com.github.ihsanbal:LoggingInterceptor:$interceptorVersion") {
        exclude group: 'org.json', module: 'json'
    }

    //Rx
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    //Di
    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"

    //Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    debugImplementation "com.android.support.test.espresso.idling:idling-concurrent:$espressoVersion"
    debugImplementation("com.jakewharton.espresso:okhttp3-idling-resource:1.0.0") {
        exclude module: 'support-annotations'
    }
}
